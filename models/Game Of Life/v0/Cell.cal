
actor Cell (init) NW, N, NE, W, E, SW, S, SE ==> Out:

	state := null;
	
	Init: action ==> Out: [state]
	do
		state := init;
	end

	Run: action [nw], [n], [ne], [w], [e], [sw], [s], [se] ==> [state]
	var
		count = sum([nw, n, ne, w, e, sw, s, se])
	do
		if state = 0 then
			if count = 3 then 
				state := 1; 
			end
		else
			if count < 2 or count > 3 then
				state := 0;
			end
		end
	end
	
	schedule fsm s0:
		s0 (Init) --> s1;
		s1 (Run) --> s1;
	end
	
    function sum (L) :
        accumulate(lambda (x, y) : x + y end, 0, L)
    end
end

