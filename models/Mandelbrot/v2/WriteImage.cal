
import java.awt.image.BufferedImage;
import javax.imageio.ImageIO;

actor WriteImage (fileName, format, W, H) R, G, B ==> :

	image = BufferedImage(W, H, BufferedImage.TYPE_INT_RGB);
	x := 0;
	y := 0;
	
	R:	action [r], [g], [b] ==>
		guard 
			y < H
		do
			image.setRGB(x, y, createPixel(r, g, b));
			x := x + 1;
			if x >= W then
				y := y + 1;
				x := 0;
			end
		end
	
	D:	action ==>
		guard
			y >= H
		var
			outputStream = createFile(fileName)
		do
			ImageIO.write(image, format, outputStream);
			closeOutputStream(outputStream);
		end
		
	schedule fsm run :
		run (R) --> run;
		run (D) --> done;
	end	
	
	function createPixel(r, g, b) : (toInt(r) * 256 + toInt(g)) * 256 + toInt(b) end
	
	function toInt(v) 
	var
		n = int(v * 256)
	:
		if n < 0 then 0 else
		if n > 255 then 255 else n end end
	end
end	
