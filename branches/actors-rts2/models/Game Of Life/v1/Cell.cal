
actor Cell (init, x, y, nSteps) NW, N, NE, W, E, SW, S, SE ==> Out, Display:

	state := null;
	steps := 0;
	generation := 0;
	
	Init: action ==> Out: [state]
	do
		state := init;
	end

	Run: action [nw], [n], [ne], [w], [e], [sw], [s], [se] ==> Out: [state]
	guard
		steps > 0
	var
		count = sum([nw, n, ne, w, e, sw, s, se])
	do
		if state = 0 then
			if count = 3 then 
				state := 1; 
			end
		else
			if count < 2 or count > 3 then
				state := 0;
			end
		end
		steps := steps - 1;
		generation := generation + 1;
	end
	
	Run: action ==> Display: [[x, y, state, generation]]
	guard
		steps <= 0
	do
		steps := nSteps;
	end
	
    function sum (L) :
        accumulate(lambda (a, b) : a + b end, 0, L)
    end
    
    schedule fsm s0:
    	s0 (Init) --> s1;
    	s1 (Run) --> s1;
    end
    
end

