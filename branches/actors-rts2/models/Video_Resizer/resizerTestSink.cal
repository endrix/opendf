/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/

// resizerTestSink.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

import ptolemy.media.Picture;
import javax.swing.JFrame;
import java.lang.System;

actor resizerTestSink ( MAXW, MAXH ) IN, PARAM ==> : 

  picture = Picture( MAXW, MAXH );
  frame = JFrame( "resizerTestSink" );
  
  initialize ==>
  do
    frame.getContentPane().add( picture );
    frame.pack();
    frame.setVisible( true );
  end
  
  FRAMEBIT = 65536;

  y;
  count := 0;
  width;
  height;

  sof: action IN:[ s ], PARAM:[ w, h ] ==>
  guard (s & FRAMEBIT) != 0 
  do
    y := 0;
    width := w;
    height := h;
    count := count + 1;
  end
  
  function yuv2rgb( py, pu, pv  )
  var
    t = ( 76306 * (py - 16) ) + 32768 : 
    [ ( t+(104597*(pv-128)))/65536, 
        ( t-((25675*(pu-128))+(53279*(pv-128))))/ 65536,
        ( t+(132201*(pu-128)))/65536
    ]
  end

  pixel.done: action ==>
  guard y >= height
  do
    println( "Displayed frame " + count);
  end
    
  pixel.draw: action IN:[ row ] repeat width ==>
  var x := 0, rgb, u, v
  do
    while x < width do
      u := row[x] & 255;
      v := row[x+1] & 255;
      rgb := yuv2rgb( row[x]/256, u, v );
      picture.setPixel( y, x, rgb[0], rgb[1], rgb[2] );
      rgb := yuv2rgb( row[x+1]/256, u, v );
      picture.setPixel( y, x+1, rgb[0], rgb[1], rgb[2] );
      x := x + 2;
    end
    picture.displayImage();
    y := y + 1;
  end

  schedule fsm sof :
    sof    ( sof        ) --> pixel;
    
    pixel  ( pixel.draw ) --> pixel;
    pixel  ( pixel.done ) --> sof;
  end
  
  priority
    pixel.done > pixel.draw;
  end
  
end