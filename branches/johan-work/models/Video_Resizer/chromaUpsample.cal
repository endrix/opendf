/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/

// chromaUpsample.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

actor chromaUpsample ( int PIXSZ ) int(size=2*PIXSZ+1) IN ==> int(size=3*PIXSZ+1) OUT : 

  int(size=2*PIXSZ) y0_u;
  int(size=3*PIXSZ) y1_uv := 0;

  function MASK( p ) :
     p & ( (1 << PIXSZ) - 1 )
  end

  int FRAME2 = 1 << (2*PIXSZ);

  int FRAME3 = 1 << (3*PIXSZ);
  function MASK3( p ) :
    p & ( FRAME3 - 1 )
  end

  sof: action IN:[ p ] ==> OUT:[ FRAME3 ]
  guard ( p & FRAME2 ) != 0
  end
  
  read_y0: action IN:[ p ] ==>
  do
    y0_u := p;
  end

  read_y1_write_y0: action IN:[ p ] ==> OUT:[ MASK3( y0_uv ) ]
  var
    int(size=3*PIXSZ) y0_uv = ( y0_u << PIXSZ ) + MASK( p )
  do
    y1_uv := ( MASK( p >> PIXSZ ) << (2*PIXSZ) )  +
             ( MASK( y0_u ) << PIXSZ ) +
             MASK( p );
  end

  write_y1: action ==> OUT:[ MASK3( y1_uv ) ]
//do
//println("Pairs " + junk );
  end

  read_y0_write_y1: action IN:[ p ] ==> OUT:[ MASK3( y1_uv ) ]
  guard ( p & FRAME2 ) = 0
  do
    y0_u := p;
//println("Pairs " + junk );
  end

    
  schedule fsm zero :
    // Waiting for any input
    zero  ( sof              ) --> zero;
    zero  ( read_y0          ) --> one;
    
    // Got the y/u pair
    one   ( sof              ) --> zero;  // (y,u) pair is lost if odd number of pixels in frame
    one   ( read_y1_write_y0 ) --> two;

    // Write the y/v pixel, keep going back-to-back if possible
    // (non-deterministic priority relationship)
    two  ( read_y0_write_y1 ) --> one;
    two  ( write_y1         ) --> zero;
  end

  priority
    sof > read_y0;
    sof > read_y1_write_y0;
    read_y0_write_y1 > write_y1;
  end
end